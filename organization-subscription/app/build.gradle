plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.liquibase.gradle' version '2.2.2'
    id 'jacoco'
}

group = 'com.hb'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

ext {
    mapstructVersion = "1.6.0.Beta1"
    currentTimestamp = new Date().format("yyyyMMddHHmmss")
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'com.hb:error-management:0.0.1'

    implementation 'org.liquibase:liquibase-core'
    implementation 'org.postgresql:postgresql:42.7.3'

    liquibaseRuntime 'info.picocli:picocli:4.7.6'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate6:4.28.0'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.3'
    liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
    liquibaseRuntime sourceSets.main.output

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

sourceSets {
    main {
        java {
            srcDirs 'build/generate-resources'
        }
    }
}

liquibase {
    activities {
        register("main") {
            changeLogFile "src/main/resources/migration/changelog/${currentTimestamp}_changelog.yml"
            url 'jdbc:postgresql://localhost:5433/organization_subscription'
            referenceUrl 'hibernate:spring:com.hb.organizationsubscription.adapters.driven.database?' +
                    'dialect=org.hibernate.dialect.PostgreSQLDialect&amp;' +
                    'hibernate.physical_naming_strategy=org.hibernate.model.naming.PhysicalNamingStrategyStandardImpl&amp;' +
                    'hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
            username 'postgres'
            password 'secret'
            driver 'org.postgresql.Driver'
        }
    }
    runList = 'main'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    reportsDirectory = layout.buildDirectory.dir('jacoco/coverage')
}

jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacoco/coverage/html')
        xml.outputLocation = layout.buildDirectory.dir('jacoco/coverage/xml/jacocoTestReport.xml').get().asFile
    }
}
